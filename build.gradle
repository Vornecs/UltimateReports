plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
    id "com.gradleup.shadow" version "9.2.2"
}

group = 'dev.xf3d3'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven { url = "https://jitpack.io" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url = "https://repo.william278.net/releases" }
    maven { url = "https://repo.opencollab.dev/main/" }
    maven { url = "https://mvn-repo.arim.space/lesser-gpl3/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.tcoded.com/releases" }
    maven { url = "https://repo.minebench.de/" }
    maven { url = "https://repo.viaversion.com" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")

    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("com.viaversion:viaversion-api:5.5.1")
    implementation("org.bstats:bstats-bukkit:3.1.0")
    implementation("de.themoep:inventorygui:1.6.6-SNAPSHOT")
    implementation("com.tcoded:FoliaLib:0.5.1")
    implementation("de.themoep:minedown-adventure:1.7.5")
    implementation("co.aikar:acf-paper:0.5.1-SNAPSHOT")

    compileOnly("net.dv8tion:JDA:6.1.1")

    compileOnly("com.zaxxer:HikariCP:7.0.2")
    implementation("net.william278:DesertWell:2.0.4")
    implementation("com.google.code.gson:gson:2.13.2")
    implementation("de.exlll:configlib-yaml:4.6.4")

    compileOnly("org.xerial:sqlite-jdbc:3.46.1.0")
    compileOnly("com.mysql:mysql-connector-j:9.2.0")
    compileOnly("com.h2database:h2:2.3.232")
    compileOnly("org.postgresql:postgresql:42.7.3")

    compileOnly("redis.clients:jedis:5.2.0")

    compileOnly("org.projectlombok:lombok:1.18.40")
    annotationProcessor("org.projectlombok:lombok:1.18.40")
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }

    build {
        dependsOn(shadowJar)
    }


    shadowJar {
        archiveFileName.set("${rootProject.name}-${rootProject.version}.jar")
        //archiveClassifier.set("main")

        exclude("com/google/errorprone/annotations/**")

        relocate("de.exlll", "dev.xf3d3.ultimatereports.libraries")
        relocate("org.bstats", "dev.xf3d3.ultimatereports.libraries.bstats")
        relocate("de.themoep.minedown", "dev.xf3d3.ultimatereports.libraries.minedown")
        relocate("com.tcoded.folialib", "dev.xf3d3.ultimatereports.libraries.folialib")
        relocate("co.aikar", "dev.xf3d3.ultimatereports.libraries.aikar")
        relocate("com.google.gson", "dev.xf3d3.ultimatereports.libraries.gson")
        relocate("org.jetbrains", "dev.xf3d3.ultimatereports.libraries.jetbrains")
        relocate("org.intellij", "dev.xf3d3.ultimatereports.libraries.intellij")
        relocate("org.json", "dev.xf3d3.ultimatereports.libraries.json")
        relocate("de.themoep.inventorygui", "dev.xf3d3.ultimatereports.libraries.inventorygui")
        relocate("net.william278.desertwell", "dev.xf3d3.ultimatereports.libraries.desertwell")
    }

    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
